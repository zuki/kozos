OUTPUT_FORMAT("elf32-h8300")
OUTPUT_ARCH(h8300h)
ENTRY("_start")

MEMORY
{
	/*
	 * reserve 256bytes space for software interrupt vector
	 * and 256bytes space for ELF header.
	 */
	ram(rwx)	: o = 0xffbf20 + 0x200, l = 0x004000 - 0x200 /* 16kb */
	bootstack(rw)	: o = 0xfffd00, l = 0x000010 /* end of RAM */
	intrstack(rw)	: o = 0xffff00, l = 0x000010 /* end of RAM */
}

SECTIONS
{
	.text :	{
		_text_start = . ;
		*(.text)
		_etext = . ;
	} > ram

	.rodata : {
		_rodata_start = . ;
		*(.strings)
		*(.rodata)
		_erodata = . ;
	} > ram

	.data : {
		_data_start = . ;
		*(.data)
		_edata = . ;
	} > ram

	.bss : {
		_bss_start = . ;
		*(.bss)
		*(COMMON)
		_ebss = . ;
	} > ram

	_end = . ;

	.bootstack : {
		_bootstack = .;
	} > bootstack

	.intrstack : {
		_intrstack = .;
	} > intrstack
}
