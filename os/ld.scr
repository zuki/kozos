OUTPUT_FORMAT("elf32-h8300")
OUTPUT_ARCH(h8300h)
ENTRY("_start")

MEMORY
{
    /* DRAM (2MB) */
    dramall(rwx)    : o = 0x400000, l = 0x200000 /* 2MB */
    ram(rwx)        : o = 0x400000 + 0x100, l = 0x200000 - 0x100

    /* internal RAM (18KB) */
    ramall(rwx)     : o = 0xffbf20, l = 0x004000 /* 16KB */
    softvec(rw)     : o = 0xffbf20, l = 0x000040 /* top of RAM */
    userstack(rw)   : o = 0xffc000, l = 0x003f00
    bootstack(rw)   : o = 0xffff00, l = 0x000000
    intrstack(rw)   : o = 0xffff00, l = 0x000000 /* end of RAM */
}

SECTIONS
{
    .softvec : {
        _softvec = . ;
    } > softvec

    .text : {
        _text_start = . ;
        *(.text)
        *(.text.*)
        _etext = . ;
    } > ram

    .rodata : {
        _rodata_start = . ;
        *(.strings)
        *(.rodata)
        *(.rodata.*)
        . = ALIGN(4);/* http://kozos.jp/books/makeos/index.html#binutils-new */
        _erodata = . ;
    } > ram

    .data : {
        _data_start = . ;
        *(.data)
        _edata = . ;
    } > ram

    .bss : {
        _bss_start = . ;
        *(.bss)
        *(COMMON)
        _ebss = . ;
    } > ram

    . = ALIGN(4);
    _end = . ;

    .freeare : {
        _freearea = . ;
    } > ram

    .userstack : {
        _userstack = . ;
    } > userstack

    .bootstack : {
        _bootstack = . ;
    } > bootstack

    .intrstack : {
        _intrstack = . ;
    } > intrstack
}
