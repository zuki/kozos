OUTPUT_FORMAT("elf32-h8300")
OUTPUT_ARCH(h8300h)
ENTRY("_start")

MEMORY
{
    /* internal ROM (512KB) */
    romall(rx)      : o = 0x000000, l = 0x080000 /* 512KB */
    vectors(r)      : o = 0x000000, l = 0x000100 /* top of ROM */
    rom(rx)         : o = 0x000100, l = 0x07ff00

    /* DRAM (2MB) */
    dramall(rwx)    : o = 0x400000, l = 0x400000 /* 2MB */

    /* internal RAM (16KB) */
    ramall(rwx)     : o = 0xffbf20, l = 0x004000 /* 16KB */
    softvec(rw)     : o = 0xffbf20, l = 0x000040 /* top of RAM */
    data(rwx)       : o = 0xffbf60, l = 0x003f90
    bootstack(rw)   : o = 0xffff00, l = 0x000000
    intrstack(rw)   : o = 0xffff00, l = 0x000000 /* end of RAM */
}

SECTIONS
{
    .vectors : {
        vector.o(.data)
    } > vectors

    .text : {
        _text_start = . ;
        *(.text)
        *(.text.*)
        _etext = . ;
    } > rom

    .rodata : {
        _rodata_start = . ;
        *(.strings)
        *(.rodata)
        *(.rodata.*)
        . = ALIGN(4);/* http://kozos.jp/books/makeos/index.html#binutils-new */
        _erodata = . ;
    } > rom

    .softvec : {
        _softvec = . ;
    } > softvec

    .data : {
        _data_start = . ;
        *(.data)
        _edata = . ;
    } > data AT> rom

    .bss : {
        _bss_start = . ;
        *(.bss)
        *(COMMON)
        _ebss = . ;
    } > data AT> rom

    . = ALIGN(4);
    _end = . ;

    .bootstack : {
        _bootstack = . ;
    } > bootstack

    .intrstack : {
        _intrstack = . ;
    } > intrstack
}
